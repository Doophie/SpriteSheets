apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

def libraryName = "SpriteSheet"
def libraryGroupId = 'ca.doophie'
def libraryArtifactId = 'spritesheets'
def libraryVersion = '0.0.1'
def gitUrl = 'https://github.com/Doophie/SpriteSheets'

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = libraryGroupId
                artifactId = libraryArtifactId
                version = libraryVersion
            }
        }
    }
}

group = libraryGroupId

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId libraryGroupId
                artifactId libraryArtifactId

                // Add your description here
                name libraryName
                description "A library for managing sprite sheets."

                // Set your license
                licenses {
                    license {
                        name "Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0"
                    }
                }
                developers {
                    developer {
                        id "doophie"
                        name "Quinn Fagan"
                        email "quinn@doophie.ca"
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                }
            }
        }
    }
}


bintray {
    user = project.property('bintray.user')
    key = project.property('bintray.token')
    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = 'SpriteSheet'
        name = libraryArtifactId
        desc = 'Library for handling and drawing sprites on a sprite sheet.'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            released = new Date()
        }
    }
}