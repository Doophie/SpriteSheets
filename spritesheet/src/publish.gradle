
def libraryName = "SpriteSheet"
def libraryGroupId = 'ca.doophie'
def libraryArtifactId = 'spritesheets'
def libraryVersion = '0.0.1'
def gitUrl = 'https://github.com/Doophie/SpriteSheets'

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "doophie"
            name "Quinn Fagan"
            email "quinn@doophie.ca"
        }
    }

    scm {
        url gitUrl
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            bundleReleaseAar(MavenPublication) {
                from components.release
                groupId = libraryGroupId
                artifactId = libraryArtifactId
                version = libraryVersion

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', "Spritesheets for stuff")
                    root.appendNode('name', libraryName)
                    root.appendNode('url', gitUrl)
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

bintray {
    user = project.property('bintray.user')
    key = project.property('bintray.token')
    publications = ['bundleReleaseAar']
    publish = true
    override = true
    pkg {
        repo = 'SpriteSheet'
        name = libraryArtifactId
        desc = 'Library for handling and drawing sprites on a sprite sheet.'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            released = new Date()
        }
    }
}